apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
ext {
    versionMajor = 1
    versionMinor = 0
    versionPatch = 1
    mGroupId = "com.buzzebees"
    mArtifactId = "pageindicatorview"
    mVersionCode = 1
    mVersionName = generateVersionName(versionMajor, versionMinor, versionPatch)
    mLibraryName = "pagein dicatorview"
    mLibraryDescription = "pagein dicatorview Library"
}

def static generateVersionCode() {
    return (System.currentTimeMillis() / 1000).toInteger()
}

def static generateVersionName(versionMajor, versionMinor, versionPatch) {
    String versionName = "${versionMajor}.${versionMinor}.${versionPatch}"
    return "${versionName}"
}

android {
    compileSdkVersion 33
    namespace 'com.rd.pageindicatorview'
    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 33
        versionCode 1
        versionName "1.0.1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.8.1'
    implementation 'androidx.core:core:1.13.1'
    implementation 'androidx.viewpager:viewpager:1.0.0'
}


// 3. Declare task for creation of android sources.
task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

def localProperties = new Properties()
localProperties.load(new FileInputStream(rootProject.file("local.properties"))) //Set env variable GPR_USER & GPR_API_KEY if not adding a properties file

// 4. Make configuration for publishing artifact.
afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                groupId mGroupId
                artifactId mArtifactId
                version mVersionName

                from components.release

                artifact androidSourcesJar

                pom {
                    name = mLibraryName
                    description = mLibraryDescription
                }
            }
        }

        // 5. Update repository details and credentials.
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/BzbsAndroid/PageIndicatorView")
                credentials {
                    username = localProperties['gpr.usr'] ?: System.getenv("GPR_USER")
                    password = localProperties['gpr.key'] ?: System.getenv("GPR_API_KEY")
                }
            }
        }
    }
    // Generate .pom file with transitive dependencies

}

// Assembling should be performed before publishing package
publish.dependsOn assemble